plugins {
    id 'jacoco'
    alias(libs.plugins.spring.boot)
}

build {
    dependsOn(tasks.bootBuildImage)
}

dependencies {
    implementation(
        project(':amqp'),
        project(':api'),
        libs.spring.boot.starter.amqp,
        libs.spring.boot.starter.data.jpa,
        libs.spring.data.envers,
        libs.spring.boot.starter.actuator,
        libs.spring.boot.starter.data.redis,
        libs.liquibase.core,
        libs.open.api,
        libs.mapstruct,
        libs.postgresql,
        libs.feign,
        libs.prometheus,
        libs.jackson.datatype.jsr310,
        libs.vault,
        libs.shedlock.spring,
        libs.shedlock.provider
    )

    annotationProcessor(
        libs.mapstruct.processor,
        libs.jpamodelgen
    )

    testImplementation(
        libs.groovy,
        libs.groovy.json,
        libs.spock.core,
        libs.spock.spring,
        libs.spring.boot.testcontainers,
        libs.testcontainers.spock,
        libs.testcontainers.postgresql,
        libs.testcontainers.vault,
        libs.testcontainers.rabbitmq,
        libs.spring.boot.cloud.stub.runner
    )
}

check {
    dependsOn(tasks.jacocoTestCoverageVerification)
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = '0.7'.toBigDecimal()
            }
        }
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}
