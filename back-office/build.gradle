plugins {
    id 'org.springframework.boot' version '2.7.4'
    id 'jacoco'
    id 'com.google.cloud.tools.jib' version '3.3.2'
    id 'net.ltgt.errorprone' version '3.1.0'
}

build {
    dependsOn(tasks.jibDockerBuild)
    dependsOn(tasks.jib)
}

jib {
    to {
        image = "osvasldas97/${name}"
        tags = [version, "${version}"]
    }
    jib.to.image "osvasldas97/${name}:${version}"
}

dependencies {
    errorprone(
        'com.google.errorprone:error_prone_core:2.20.0',
        'tech.picnic.error-prone-support:error-prone-contrib:0.12.0',
        'tech.picnic.error-prone-support:refaster-runner:0.12.0'
    )

    implementation(
        project(':amqp'),
        project(':api'),
        libs.springBootStarterAmqp,
        libs.springBootStarterDataJpa,
        libs.springDataEnvers,
        libs.springBootStarterActuator,
        libs.springBootStarterDataRedis,
        libs.liquibaseCore,
        libs.openApi,
        libs.mapstruct,
        libs.postgresql,
        libs.feign,
        libs.prometheus,
        libs.jacksonDatatypeJsr310,
        libs.vault,
        libs.shedlockSpring,
        libs.shedlockProvider
    )

    annotationProcessor(
        libs.mapstructProcessor,
        libs.jpamodelgen
    )

    testImplementation(
        libs.groovy,
        libs.spockCore,
        libs.spockSpring,
        libs.testcontainers,
        libs.testcontainersSpock,
        libs.testcontainersPostgresql,
        libs.testcontainersVault,
        libs.testcontainersRabbitmq,
        libs.springTestRedis,
        libs.wiremock
    )
}

check {
    dependsOn(tasks.jacocoTestCoverageVerification)
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = '0.7'.toBigDecimal()
            }
        }
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml { enabled true }
        html { enabled false }
        csv { enabled false }
        xml.destination file("${buildDir}/reports/jacoco.xml")
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone.disableWarningsInGeneratedCode = true
    options.errorprone {
        disableWarningsInGeneratedCode = true
        String rules = error_prone_rules.join(',')
        errorproneArgs.addAll(
            "-XepPatchChecks:Refaster,$rules",
            '-XepPatchLocation:IN_PLACE',
            '-XepOpt:Refaster:NamePattern=(ImmutableListOf2|ImmutableMapOf1)'
        )
    }
}