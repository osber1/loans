plugins {
    id 'io.spring.dependency-management' version '1.1.0'
    id 'groovy'
    id 'checkstyle'
    id 'codenarc'
    id 'com.github.spotbugs' version '5.0.12'
    id 'org.sonarqube' version '4.0.0.2929'
}

apply from: 'gradle/dependencies.gradle'

allprojects {
    group = 'io.osvaldas'
    version = '1.0-SNAPSHOT'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'groovy'
    apply plugin: 'checkstyle'
    apply plugin: 'codenarc'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'org.sonarqube'

    dependencyManagement {
        imports {
            mavenBom libs.springBootDependencies
            mavenBom libs.springBootCloud
        }
    }

    spotbugs {
        excludeFilter = file("${rootDir}/config/spotbugs/exclude.xml")
    }

    dependencies {
        implementation(
            libs.lombok,
            libs.springBootStarterWeb,
            libs.springBootStarterBootstrap,
            libs.springBootStarterValidation
        )

        compileOnly(
            libs.lombok
        )

        annotationProcessor(
            libs.lombok,
            libs.springBootConfigurationProcessor
        )

        testImplementation(
            libs.groovy,
            libs.spockCore,
            libs.spockSpring,
            libs.springBootStarterTest
        )
    }
}